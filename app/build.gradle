apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: "androidx.navigation.safeargs.kotlin"

configurations {
    // referenced in some portable lib. use android internal instead
    compile.exclude group: 'commons-logging'
}

android {
    androidExtensions {
        experimental = true
    }
    signingConfigs {
        config {
            keyAlias 'yzune'
            keyPassword '11166511'
            storeFile file('/Volumes/Document/yzune.jks')
            storePassword '11166511'
        }
    }
    compileSdkVersion 29
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    flavorDimensions "versionCode"
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 90
        versionName "3.621"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        packagingOptions {
            exclude 'META-INF/library_release.kotlin_module'

            // prevent duplicate files
            exclude 'META-INF/DEPENDENCIES.txt'
            exclude 'META-INF/LICENSE.txt'
            exclude 'META-INF/NOTICE.txt'
            exclude 'META-INF/NOTICE'
            exclude 'META-INF/LICENSE'
            exclude 'META-INF/DEPENDENCIES'
            exclude 'META-INF/notice.txt'
            exclude 'META-INF/license.txt'
            exclude 'META-INF/dependencies.txt'
            exclude 'META-INF/LGPL2.1'
            exclude 'META-INF/ASL2.0'

            //!!! android buildToolsVersion 19.0.3 does not support exclude with wildcards yet :-(
            exclude('org/apache/commons/codec/language/bm/*.txt')

            exclude 'lib/*/libRSSupport.so'
            exclude 'lib/*/librsjni.so'
            exclude 'lib/*/librsjni_androidx.so'
        }
        ndk {
            // 选择要添加的对应cpu类型的.so库。
            abiFilters 'armeabi-v7a', "arm64-v8a", "x86"
            // 还可以添加 'armeabi-v7a', 'armeabi-v8a','x86', 'x86_64', 'mips', 'mips64'
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFile 'proguard-android.txt'
            proguardFile 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }
    buildToolsVersion '29.0.3'

    productFlavors {
        google {
            applicationId "com.suda.yzune.wakeupschedule.pro"
            resValue "string", "app_name", "WakeUp课程表Pro"
            //AndroidManifest.xml中定义的meta-data标签,
            //需要和meta-data中的android:value="${CHANNEL}对应
            manifestPlaceholders = [CHANNEL: "google"]
            //Build中生成一个字符用来区分不同渠道的不同策略
            buildConfigField "String", "CHANNEL", "\"google\""
        }
        huawei {
            applicationId "com.suda.yzune.wakeupschedule"
            resValue "string", "app_name", "WakeUp课程表"
            //AndroidManifest.xml中定义的meta-data标签,
            //需要和meta-data中的android:value="${CHANNEL}对应
            manifestPlaceholders = [CHANNEL: "huawei"]
            //Build中生成一个字符用来区分不同渠道的不同策略
            buildConfigField "String", "CHANNEL", "\"huawei\""
        }
        normal {
            applicationId "com.suda.yzune.wakeupschedule"
            resValue "string", "app_name", "WakeUp课程表"
            manifestPlaceholders = [CHANNEL: "normal"]
            buildConfigField "String", "CHANNEL", "\"normal\""
        }
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    def lifecycle_version = "2.2.0"
    def appCenterSdkVersion = "2.5.1"
    def room_version = "2.2.3"

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation "androidx.appcompat:appcompat:1.2.0-alpha02"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.3"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.3'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'com.google.android.material:material:1.2.0-alpha04'
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation 'com.squareup.retrofit2:retrofit:2.7.1'
    implementation 'com.github.GrenderG:Toasty:1.4.2'
    implementation 'org.jsoup:jsoup:1.12.2'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'cn.carbswang.android:NumberPickerView:1.2.0'
    implementation('com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.0-beta11') {
        exclude group: 'androidx'
    }
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    kapt 'com.github.bumptech.glide:compiler:4.11.0'
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation 'com.github.doyaaaaaken:kotlin-csv-jvm:0.7.3'
    implementation 'com.github.jahirfiquitiva:TextDrawable:1.0.3'
    implementation 'com.bigkoo:quicksidebar:1.0.3'
    implementation 'com.timehop.stickyheadersrecyclerview:library:0.4.3@aar'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation('net.sf.biweekly:biweekly:0.6.3') {
        exclude module: 'jackson-core'
        exclude group: 'com.fasterxml.jackson.core'
    }
    implementation "com.microsoft.appcenter:appcenter-analytics:$appCenterSdkVersion"
    implementation "com.microsoft.appcenter:appcenter-crashes:$appCenterSdkVersion"
    implementation 'com.github.sephiroth74:android-target-tooltip:2.0.4'
}
